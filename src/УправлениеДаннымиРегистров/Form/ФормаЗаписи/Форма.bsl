
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.Заголовок;
	
	СоздатьЭлементы();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементы() 
	
	НовыеРеквизиты = Новый Массив;
	
	Для Каждого ОписаниеПоля Из Параметры.Поля Цикл
		
		Имя = "Запись" + ОписаниеПоля.Имя;
		НовыйРеквизит = Новый РеквизитФормы(Имя, ОписаниеПоля.ТипЗначения,, ОписаниеПоля.Заголовок);
		НовыеРеквизиты.Добавить(НовыйРеквизит);
		
		ИменаРеквизитов.Добавить(Имя, ОписаниеПоля.Имя);
		
	КонецЦикла;

	ИзменитьРеквизиты(НовыеРеквизиты);
	
	
	Для Каждого ОписаниеПоля Из Параметры.Поля Цикл
		
		Имя = "Запись" + ОписаниеПоля.Имя;
			
		НовыйЭлемент = Элементы.Добавить(Имя, Тип("ПолеФормы"), ЭтаФорма);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным = Имя;
		
		Если Параметры.ДанныеЗаписи <> Неопределено Тогда
			ЭтаФорма[Имя] = Параметры.ДанныеЗаписи[ОписаниеПоля.Имя];	
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Новый Структура;
	Для Каждого КлючЗначение Из ИменаРеквизитов Цикл
		Результат.Вставить(КлючЗначение.Представление, ЭтаФорма[КлючЗначение.Значение]);	
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры
