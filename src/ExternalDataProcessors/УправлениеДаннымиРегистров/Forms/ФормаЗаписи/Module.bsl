#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок 		 = Параметры.Заголовок;
	ОтображатьФлажок = Параметры.ОтображатьФлажок;
	
	Если Параметры.ОписаниеПолей = Неопределено Тогда
		ВызватьИсключение "Не передано описание полей";
	КонецЕсли;
	
	СоздатьЭлементы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Новый Структура;
	Для Каждого КлючЗначение Из ИменаРеквизитов Цикл
		Если ОтображатьФлажок 
			И ЭтотОбъект["ФлажокЗапись" + КлючЗначение.Представление]
			Или Не ОтображатьФлажок Тогда
			Результат.Вставить(КлючЗначение.Представление, ЭтотОбъект[КлючЗначение.Значение]);	
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриИзмененииРеквизита(Элемент)
	ЭтотОбъект["Флажок" + Элемент.Имя] = ЗначениеЗаполнено(ЭтотОбъект[Элемент.Имя]) Или ЭтотОбъект["Флажок" + Элемент.Имя];	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементы() 
	
	НовыеРеквизиты = Новый Массив;
	
	Для Каждого ОписаниеПоля Из Параметры.ОписаниеПолей Цикл
		
		Имя = "Запись" + ОписаниеПоля.Имя;
		
		НовыйРеквизит = Новый РеквизитФормы(Имя, ОписаниеПоля.ТипЗначения,, ОписаниеПоля.Заголовок);
		НовыеРеквизиты.Добавить(НовыйРеквизит);
		
		НовыйРеквизит = Новый РеквизитФормы("Флажок" + Имя, Новый ОписаниеТипов("Булево"));
		НовыеРеквизиты.Добавить(НовыйРеквизит);
		
		ИменаРеквизитов.Добавить(Имя, ОписаниеПоля.Имя);
		
	КонецЦикла;

	ИзменитьРеквизиты(НовыеРеквизиты);
		
	Для Каждого ОписаниеПоля Из Параметры.ОписаниеПолей Цикл
		
		Имя = "Запись" + ОписаниеПоля.Имя;
		
		ЭлементГруппа = Элементы.Добавить("Группа" + Имя, Тип("ГруппаФормы"), ЭтотОбъект);
		ЭлементГруппа.Вид 				  = ВидГруппыФормы.ОбычнаяГруппа;
		ЭлементГруппа.Группировка 		  = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		ЭлементГруппа.Отображение 		  = ОтображениеОбычнойГруппы.Нет;
		ЭлементГруппа.ОтображатьЗаголовок = Ложь;
		
		Если ОтображатьФлажок Тогда
			НовыйЭлемент = Элементы.Добавить("Флажок" + Имя, Тип("ПолеФормы"), ЭлементГруппа);
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
			НовыйЭлемент.ПутьКДанным = "Флажок" + Имя;	
			НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		КонецЕсли;
			
		НовыйЭлемент = Элементы.Добавить(Имя, Тип("ПолеФормы"), ЭлементГруппа);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным = Имя;
		НовыйЭлемент.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизита");
		
		Если Параметры.ДанныеЗаписи <> Неопределено Тогда
			ЭтотОбъект[Имя] = Параметры.ДанныеЗаписи[ОписаниеПоля.Имя];	
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти